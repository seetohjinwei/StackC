// The standard library for StackC

def false 0 end
def true 1 end

def dup2 // A, B -> A, B, A, B
  over over
end

def rot3 // A, B, C -> C, A, B
  rot rot
end

def cr // None -> None
  "\n" print
end

// peek operation
def , // int -> int
  dup .
end

// haven't removed as a primitive
def print2
  while dup 0 > then
    1 -
    swap emit
  end
  drop drop
end

def reverseN // n ints, n -> n ints
  "not possible without an additional stack?" print
end

// MATH OPERATIONS (math library) //

def neg // a -> -a
  0 swap -
end

def sumN // n ints, n -> int
  0
  while swap dup 0 > then
    1 -
    swap rot +
  end
  drop
end

def factorial // n -> n!
  1 swap
  while dup 0 > then
    dup rot *
    swap 1 -
  end
  drop
end

def pow // a, b -> a^b
  swap dup
  while rot dup 1 > then
    1 -
    rot rot
    over *
  end
  drop swap drop
end

def abs // a -> |a|
  if dup 0 < then
    neg
  end
end

def max // a, b -> max(a, b)
  if dup2 > then
    drop
  elseif 1 then
    swap drop
  end
end

def min // a, b -> max(a, b)
  if dup2 < then
    drop
  elseif 1 then
    swap drop
  end
end

// using Euclidean algorithm (supports negative numbers)
def gcd // a, b -> gcd(a, b)
  while dup then
    swap over %
  end
  drop abs
end

def isprime
  if dup 2 = then
    drop 1
  elseif dup 2 < then
    drop 0
  elseif dup 2 % 0 = then
    drop 0
  elseif 1 then
    1 swap
    3
    // change to SQRT eventually (when we have floats?)
    while over 2 / over > then
      if dup2 % 0 = then
        drop drop drop 0 0 0
      elseif 1 then
        2 +
      end
    end
    drop drop
  end
end

// -- TESTS -- //
// temporary because no include yet

if 14 5 pow 537824 = then
  "14^5 Correct\n" print
elseif 1 then
  "14^5 wrong\n" print
end

if 2 3 pow 8 = then
  "2^3 Correct\n" print
elseif 1 then
  "2^3 wrong\n" print
end

if 26 7 pow 8031810176 = then
  "26^7 Correct\n" print
elseif 1 then
  "26^7 wrong\n" print
end

"ABC" print2
"\nnew line\t\n\\\n" print2

if 1 2 3 3 sumN 6 = then
  "1 2 3 3 sumN Correct\n" print
elseif 1 then
  "1 2 3 3 sumN wrong\n" print
end

if 123 456 789 0 4 sumN 1368 = then
  "123 456 789 0 4 sumN Correct\n" print
elseif 1 then
  "123 456 789 0 4 sumN wrong\n" print
end

if 5 factorial 120 = then
  "5 factorial Correct\n" print
elseif 1 then
  "5 factorial wrong\n" print
end

if 12 factorial 479001600 = then
  "12 factorial Correct\n" print
elseif 1 then
  "12 factorial wrong\n" print
end

2 abs . cr
0 abs . cr
-2 abs . cr

1 2 max . cr
2 1 max . cr

1 2 min . cr
2 1 min . cr

1071 462 gcd . cr // 21
-531 789 gcd . cr // 3
